import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.springframework.boot' version '3.0.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.7.22'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.7.22'
    id "org.jetbrains.kotlin.kapt" version "1.7.22"
}

sourceCompatibility = '17'

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "java"

    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.springframework.boot"
    apply plugin: "org.jetbrains.kotlin.plugin.spring"

    apply plugin: "kotlin"
    apply plugin: "kotlin-spring" //all-open
    apply plugin: "kotlin-jpa"
    apply plugin: "kotlin-kapt"

    dependencies {
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
    }

    tasks.withType(KotlinCompile) {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '17'
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}


project(':domain'){
    bootJar.enabled = false
    jar.enabled = true
}

project(':application'){
    bootJar.enabled = false
    jar.enabled = true
    dependencies {
        implementation project(path: ':domain')
    }
}

project(':infrastructure'){
    dependencies {
        implementation project(path: ':domain')
        implementation project(path: ':application')
    }
}

